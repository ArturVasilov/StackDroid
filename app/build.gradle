apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE.txt'
    }

    defaultConfig {
        applicationId "ru.arturvasilov.stackexchangeclient"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode rootProject.versionCode
        versionName rootProject.versionName

        buildConfigField "String", "CLIENT_ID", rootProject.ext.clientId
        buildConfigField "String", "KEY_SECRET", rootProject.ext.keySecret
        buildConfigField "String", "API_ENDPOINT", '"https://api.stackexchange.com/2.2/"'

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        debug {
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storeFile file('debug.keystore')
            storePassword 'android'
        }
        release {
            keyAlias rootProject.ext.keyAlias
            keyPassword rootProject.ext.keyPassword
            storeFile file(rootProject.ext.storeFile)
            storePassword rootProject.ext.storePassword
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled true
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions 'buildType'

    productFlavors {
        dev {
            dimension 'buildType'
            signingConfig signingConfigs.debug
        }

        prod {
            signingConfig signingConfigs.release
            dimension 'buildType'
        }
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    testOptions {
        unitTests.all { jvmArgs '-noverify' }
    }
}

dependencies {
    def supportVersion = '25.1.0'
    def okhttpVersion = '3.5.0'
    def retrofitVersion = '2.1.0'
    def firebaseVersion = '10.0.1'

    compile "com.android.support:support-v4:$supportVersion"
    compile "com.android.support:support-v13:$supportVersion"
    compile "com.android.support:appcompat-v7:$supportVersion"
    compile "com.android.support:design:$supportVersion"
    compile "com.android.support:recyclerview-v7:$supportVersion"
    compile "com.android.support:gridlayout-v7:$supportVersion"

    compile "com.squareup.okhttp3:okhttp:$okhttpVersion"
    compile "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"

    compile "com.squareup.retrofit2:retrofit:$retrofitVersion"
    compile "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    compile "com.squareup.retrofit2:adapter-rxjava:$retrofitVersion"

    compile 'com.google.code.gson:gson:2.8.0'

    compile 'ru.arturvasilov:sqlite:0.1.8'

    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.1.10'
    compile 'ru.arturvasilov:rx-loader:0.1.3'

    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.0.2'

    compile 'com.github.orhanobut:hawk:1.23'

    compile 'me.relex:circleindicator:1.2.1@aar'

    compile "com.google.firebase:firebase-core:$firebaseVersion"
    compile "com.google.firebase:firebase-messaging:$firebaseVersion"
    compile "com.google.firebase:firebase-appindexing:$firebaseVersion"

    compile('com.crashlytics.sdk.android:crashlytics:2.6.1@aar') {
        transitive = true;
    }

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.0.87-beta'

    androidTestCompile "com.android.support:appcompat-v7:$supportVersion"
    androidTestCompile "com.android.support:design:$supportVersion"
    androidTestCompile "com.android.support:support-annotations:$supportVersion"
    androidTestCompile "com.android.support:support-v4:$supportVersion"
    androidTestCompile "com.android.support:recyclerview-v7:$supportVersion"

    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.android.support.test.espresso:espresso-intents:2.2.2'
    androidTestCompile 'com.android.support.test.espresso:espresso-contrib:2.2.2'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
}

apply from: 'jacoco.gradle'
apply plugin: 'com.google.gms.google-services'
